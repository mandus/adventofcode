#!/usr/bin/env goal 

fn:"input.txt"
(2=#ARGS) and fn:ARGS@1

d:=-read fn
l:""\"|"/d / mark the edges with '|' to easily know later if I hit them 
s:1+&*d; sz:#l
mv:("i"$2 exp !4)!(s;-1;-s;1)
(e;m;p):{~x?l}'""\"|#^"
p:*&p / start position
out:{(x<0) or x>sz-1}
turn:15!2*

/ for part2
i:!4
i2b:{x and :2!x,o[-2!x]} / integer to binary array
/b2i:{"i"$+/x*(2 exp (|!#x))} / binary array to integer; not needed for the problem
ibip:{(x and y) and +/((i2b x)@i)*(i2b y)@i} / inner prod of binary arrays for two integers; index with i since length may differ

v:sz#0 / visited positions
iter:{[p;d] /position; direction
 (ibip[d;v[p]]) and :0 / been here in same direction before; exit 
 v[p]+:d; np:p - mv[d] / update visited; next position
 ((out[np]) or e[np]) and :#?&v / leaving the area 
 ?[m[np];:o[p;turn d];:o[np;d]] / hit marker, turn and try again - else continue
}

p1: iter[p;1] / implicitly updates v which is used for part2
say "part1: $p1"
p2:+/{[am] v::sz#0; m[am]:1; n:iter[p;1]; m[am]:0; n=0}'?&v
say "part2: $p2"
