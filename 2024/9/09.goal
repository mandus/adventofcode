#!/usr/bin/env goal
fn:"input.txt"
(2=#ARGS) and fn:ARGS@1
w:#d:"i"$""\-read fn
(sp;fp):(-1+w>)^'{(1+x;x)}2*!w
cp:+(d;0,-1_+\d)
(f;s):(cp@)'(fp;sp)
f:+(*+f;*|+f;!#f)

dfr:{[x;y;l]
 (lx;ly):#'(x;y); and[or[~lx;~ly];:l];
 (sc;sp):*x; (fc;fp;fi):*y; m:sc&fc;
 and[sp>fp;(l,:y; :l)];
 l,:+(m;sp;fi);
 ?[
   m<fc;:o[1_x;((+((fc-m);fp;fi)),1_y);l]
   m<sc;:o[((+((sc-m),(sp+m))),1_x);1_y;l]
   :o[1_x;1_y;l]];
}
p1:+/{(c;p;i):x; +/i*(p+!c)}'dfr[s;|f;+(0 0 0)]
say "part 1: $p1"

dfr2:{[s;f;l]
 and[~(#f);:l] / done
 a:*+s; (fc;fp;fi):*f; / a is "avail free space"
 or[c:(fc>)^a;(l,:+(*f); :o[s;1_f;l])] / no space avail
 idx:a?*c; (sc;sp):s idx
 and[sp>fp;(l,:+(*f); :o[s;1_f;l])] / avail space is right, so do not move
 l,:+(fc;sp;fi)
 s:@[s;idx;(-fc;fc)+]
 :o[s;1_f;l]
}
p2:+/{(c;p;i):x; +/i*(p+!c)}'dfr2[s;|f;+(0 0 0)]
say "part 2: $p2"
